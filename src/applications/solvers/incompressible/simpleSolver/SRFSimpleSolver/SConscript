#Copyright (C) 2014 Applied CCM
import subprocess
Import('globalEnv')

# Clone the global environment so changes made he are not pass onto subsequent 
# libraries
SRFSimpleSolEnv = globalEnv.Clone()

# Path to source
src = SRFSimpleSolEnv['LIB_SRC']

#===============================================================================
# Additions to compiler flags for this executable
#===============================================================================
SRFSimpleSolEnv.Append(CCFLAGS = '')

#===============================================================================
# Additions to include directories for this executable
#===============================================================================
lib_inc = [src + '/turbulenceModels',
src + '/turbulenceModels/incompressible/turbulenceModel/lnInclude', 
src + '/turbulenceModels/incompressible/RAS/RASModel', 
src + '/turbulenceModels/incompressible/RAS/lnInclude',
src + '/transportModels',
src + '/transportModels/incompressible/singlePhaseTransportModel',
src + '/transportModels/incompressible/lnInclude'
]
SRFSimpleSolEnv.Prepend(CPPPATH = lib_inc)

#===============================================================================
# Additions to link library paths for this executable
#===============================================================================
lib_link_path = []
SRFSimpleSolEnv.Prepend(LIBPATH = lib_link_path)

#===============================================================================
# Additions to link libraries for this executable
#===============================================================================
if SRFSimpleSolEnv['WHICH_OS'] == "windows":
  SRFSimpleSolEnv.Prepend(LINKFLAGS = ' -u incompressibleRASModelsLoad')
  lib_link = ['libincompressibleTurbulenceModel', 'libincompressibleRASModels',
  'libincompressibleTransportModels']
else:
  lib_link = ['incompressibleTurbulenceModel', 'incompressibleRASModels',
  'incompressibleTransportModels']
  
SRFSimpleSolEnv.Append(LIBS = lib_link)

#===============================================================================
# Sources for this executable
#===============================================================================
src_files = Split("""
  SRFSimpleSolver.cpp
  """)

#===============================================================================
# Build this executable
#===============================================================================
binSRFSimpleSol = SRFSimpleSolEnv.Program(target = 'SRFSimpleSolver',
source = src_files)

#===============================================================================
# Install this executable
#===============================================================================
install_dir = SRFSimpleSolEnv['BIN_PLATFORM_INSTALL']
SRFSimpleSolEnv.Alias('install', install_dir)
SRFSimpleSolEnv.Install(install_dir, binSRFSimpleSol)
