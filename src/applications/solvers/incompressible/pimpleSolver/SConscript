#Copyright (C) 2014 Applied CCM
import subprocess
Import('globalEnv')

# Clone the global environment so changes made he are not pass onto subsequent 
# libraries
pimpleSolEnv = globalEnv.Clone()

# Path to source
src = pimpleSolEnv['LIB_SRC']

#===============================================================================
# Additions to include directories for this executable
#===============================================================================
lib_inc = [src + '/turbulenceModels/incompressible/turbulenceModel',
src + '/turbulenceModels/incompressible/turbulenceModel/lnInclude', 
src + '/transportModels',
src + '/transportModels/incompressible/singlePhaseTransportModel',
src + '/transportModels/incompressible/lnInclude',
src + '/fvOptions/lnInclude',
src + '/sampling/lnInclude'
]
pimpleSolEnv.Prepend(CPPPATH = lib_inc)

#===============================================================================
# Additions to link libraries for this executable
#===============================================================================
if pimpleSolEnv['WHICH_OS'] == "windows":
  pimpleSolEnv.Prepend(LINKFLAGS = ' -u incompressibleRASModelsLoad  -u incompressibleLESModelsLoad -u incompressibleVLESModelsLoad')

lib_link = ['libincompressibleTurbulenceModel', 'libincompressibleRASModels',
'libincompressibleLESModels','libincompressibleTransportModels', 'fvOptions', 'sampling','libincompressibleVLESModels']
pimpleSolEnv.Append(LIBS = lib_link)

#===============================================================================
# Sources for this executable
#===============================================================================
src_files = Split("""
  pimpleSolver.cpp
  """)

#===============================================================================
# Build this executable
#===============================================================================
binpimpleSol = pimpleSolEnv.Program(target = 'pimpleSolver',
source = src_files)

#===============================================================================
# Install this executable
#===============================================================================
install_dir = pimpleSolEnv['BIN_PLATFORM_INSTALL']
pimpleSolEnv.Alias('install', install_dir)
pimpleSolEnv.Install(install_dir, binpimpleSol)
